let bench_instances = (
    from instance
    group [problem] (
        sort timestamp
        take 5
    )
)

let first_submission = (
    from submission
    group [problem, solution] (
        aggregate [
            timestamp = min timestamp
        ]
    )
)

from instance = bench_instances
join submission = first_submission [submission.problem == instance.problem]
join side:left execution [
    execution.instance == instance.id,
    execution.solution == submission.solution,
]
filter execution.id == null
sort [submission.timestamp, instance.timestamp]

join problem [problem.id == submission.problem]
join solution [solution.id == submission.solution]
select [
    instance_seed = instance.seed,
    solution_hash = solution.file_hash,
    problem_hash = problem.file_hash,
]
